#configure keycloak provider
provider "keycloak" {
  client_id                = "admin-cli"
  username                 = "admin"
  password                 = "admin"
  url                      = "https://keycloak.wam.beninanutshell.com"
  tls_insecure_skip_verify = true

}


locals {
  realm_id = "master"
  groups   = ["grafana-dev", "grafana-admin", "argo-admin", "argo-dev"]
  user_groups = {
    user-dev    = ["grafana-dev", "argo-dev"]
    user-admin  = ["grafana-admin", "argo-admin"]
    user-viewer = []
  }
}
# create groups
resource "keycloak_group" "groups" {
  for_each = toset(local.groups)
  realm_id = local.realm_id
  name     = each.key

  depends_on = [
    helm_release.traefik, helm_release.keycloak
  ]
}
# create users
resource "keycloak_user" "users" {
  for_each       = local.user_groups
  realm_id       = local.realm_id
  username       = each.key
  enabled        = true
  email          = "${each.key}@beninanutshell.com"
  email_verified = true
  first_name     = each.key
  last_name      = each.key
  initial_password {
    value = each.key
  }

  depends_on = [
    helm_release.traefik, helm_release.keycloak
  ]
}
# configure use groups membership
resource "keycloak_user_groups" "user_groups" {
  for_each  = local.user_groups
  realm_id  = local.realm_id
  user_id   = keycloak_user.users[each.key].id
  group_ids = [for g in each.value : keycloak_group.groups[g].id]

  depends_on = [
    helm_release.traefik, helm_release.keycloak
  ]
}
# create groups openid client scope
resource "keycloak_openid_client_scope" "groups" {
  realm_id               = local.realm_id
  name                   = "groups"
  include_in_token_scope = true
  gui_order              = 1

  depends_on = [
    helm_release.traefik, helm_release.keycloak
  ]
}
resource "keycloak_openid_group_membership_protocol_mapper" "groups" {
  realm_id        = local.realm_id
  client_scope_id = keycloak_openid_client_scope.groups.id
  name            = "groups"
  claim_name      = "groups"
  full_path       = false

  depends_on = [
    helm_release.traefik, helm_release.keycloak
  ]
}
# create keycloak openid client
resource "keycloak_openid_client" "grafana" {
  realm_id              = local.realm_id
  client_id             = "grafana"
  name                  = "grafana"
  enabled               = true
  access_type           = "CONFIDENTIAL"
  client_secret         = "grafana-client-secret"
  standard_flow_enabled = true
  valid_redirect_uris = [
    "https://grafana.wam.beninanutshell.com/login/generic_oauth"
  ]

  depends_on = [
    helm_release.traefik, helm_release.keycloak
  ]
}
# configure keycloak openid client default scopes
resource "keycloak_openid_client_default_scopes" "grafana" {
  realm_id  = local.realm_id
  client_id = keycloak_openid_client.grafana.id
  default_scopes = [
    "profile",
    "email",
    keycloak_openid_client_scope.groups.name,
  ]
  depends_on = [
    helm_release.traefik, helm_release.keycloak
  ]
}

# create argocd openid client
resource "keycloak_openid_client" "argocd" {
  realm_id              = local.realm_id
  client_id             = "argocd"
  client_secret         = "argocd-client-secret"
  name                  = "argocd"
  enabled               = true
  access_type           = "CONFIDENTIAL"
  standard_flow_enabled = true
  valid_redirect_uris = [
    "https://argocd.wam.beninanutshell.com/auth/callback"
  ]
}

# configure argocd openid client default scopes
resource "keycloak_openid_client_default_scopes" "client_default_scopes_argo" {
  realm_id  = local.realm_id
  client_id = keycloak_openid_client.argocd.id
  default_scopes = [
    "profile",
    "email",
    "roles",
    "web-origins",
    keycloak_openid_client_scope.groups.name,
  ]
}

# output argocd openid client secret
output "client-secret" {
  value     = keycloak_openid_client.argocd.client_secret
  sensitive = true
}